plugins {
    id 'org.springframework.boot' version '2.4.5'
    id 'com.palantir.docker' version '0.26.0'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'java'
}

group = 'net.gendercomics.api'
version = '2.3.0'
sourceCompatibility = '11'

ext {
    jacksonVersion = '2.9.10'
    junitVersion = '5.7.1'
    keycloakVersion = '6.0.1'
    lombokVersion = '1.18.22'
    springBootVersion = '2.4.5'
    springSecurityVersion = '5.4.6'
    testContainersVersion = '1.15.3'
    swaggerVersion = '2.9.2'
}

ext['log4j2.version'] = '2.16.0'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

springBoot {
    buildInfo()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    // Spring
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Jackson
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"

    // Swagger
    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    // Keycloak
    implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"

    // Test
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.security:spring-security-test:${springSecurityVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testCompile "org.testcontainers:junit-jupiter:${testContainersVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}"
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "${project.group}/${bootJar.archiveBaseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}
